const STDIN : 0
const STDOUT : 1

const RDONLY : 0
const WRONLY : 1
const CREAT : 64
const TRUNC : 512

type File : struct {
    descriptor: uint
}

proc open_file_for_writing(path: *[]byte): File {
    return @build(File, @syscall3(2, path, WRONLY + CREAT + TRUNC, 452));
}

proc open_file_for_reading(path: *[]byte): File {
    return @build(File, @syscall3(2, path, RDONLY, 452));
}

proc close_file(file: File) {
    var _: uint = @syscall1(3, file.descriptor);
}

proc create_file_writer(file: *File): Writer {
    return @build(Writer, @cast(ptr, file), write_file);
}

proc create_file_reader(file: *File): Reader {
    return @build(Reader, @cast(ptr, file), read_file);
}

proc write_file(data: ptr, string: *[]byte, length: uint) {
    var file: *File = @cast(*File, data);
    var _: uint = @syscall3(1, file.descriptor, string, length);
}

proc read_file(pointer: ptr, string: *[]byte, length: uint): uint {
    var file: *File = @cast(*File, pointer);
    return @syscall3(0, file.descriptor, string, length);
}

proc get_file_size(file: File): uint {
    var stat: Stat;
    sys_fstat(file.descriptor, &stat);
    return stat.size;
}

proc read_file_to_string(file: File, allocator: Allocator): String {
    var size: uint = get_file_size(file);
    var result: ptr = allocate_size(allocator, size);

    var reader: Reader = create_file_reader(&file);
    var _: uint = read_string(reader, @cast(*[]byte, result), size);

    return @build(String, @cast(*[]byte, result), size);
}
